apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "iot.com.smartmirror"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['./assets']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Download model zip file into ../../assets directory
// unzip it to demo project's own ./assets directory
import de.undercouch.gradle.tasks.download.Download
task downloadFile(type: Download) {
    src 'https://storage.googleapis.com/download.tensorflow.org/models/inception5h.zip'
    dest projectDir.toString() + '/../../assets/inception.zip'
}
task unzip(type: Copy) {
    from zipTree(projectDir.toString() + '/../../assets/inception.zip')
    into file(projectDir.toString() + '/assets')
}
unzip.dependsOn downloadFile

project.afterEvaluate {
    if (!(new File(projectDir.toString() + '/assets')).exists()) {
        preBuild.dependsOn unzip
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    provided 'org.projectlombok:lombok:1.16.18'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // the following is the dependency of android things.
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    // TO_THINGS this block necessary when SmartMirror work on android things.
    //provided 'com.google.android.things:androidthings:0.4-devpreview'
    //compile 'com.google.android.things.contrib:driver-button:0.3'

    // following is the dependency of tensorflow
    compile(name: 'Tensorflow-Android-Inference-alpha-debug', ext: 'aar')
    compile project(':openCVLibrary320')

    // JSON Parser
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.6'
    compile 'com.bluelinelabs:logansquare:1.3.6'

    // use reactive android
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'

    // use retrofit for rest image parse api.
    compile 'com.squareup.retrofit:retrofit:1.9.0'
}
